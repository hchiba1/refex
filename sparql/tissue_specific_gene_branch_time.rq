#!/usr/bin/env spang2
# @endpoint orthodev
# @option -a
# @param specific_tissue=v24_40

PREFIX orth: <http://purl.org/net/orth#>
PREFIX homologene: <https://ncbi.nlm.nih.gov/homologene/>

SELECT ?human_gene (max(?time) as ?branch_time_mya)
WHERE {
  ?grp orth:inDataset homologene: ;
      orth:hasHomologousMember ?human_gene, ?gene .
  ?human_gene orth:taxon taxid:9606 .
  ?gene orth:taxon ?taxid .
  ?taxid hop:branchTimeMya ?time .
  {
    SELECT DISTINCT ?human_gene (GROUP_CONCAT(DISTINCT ?tissue; separator=",") AS ?tissues)
    WHERE {
      ?human_gene refexo:affyProbeset/refexo:isPositivelySpecificTo refexo:{{specific_tissue}} , ?tissue .
    }
  }
  FILTER (?tissues = str(refexo:{{specific_tissue}}))
}
ORDER BY ?branch_time_mya
