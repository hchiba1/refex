#!/usr/bin/env spang2
# @endpoint orthodev
# @option -ac

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX refexo: <http://purl.jp/bio/01/refexo#>

SELECT ?v40 ?v40_label ?count
WHERE {
  ?v40 a refexo:AnatomicalClassification40 ;
     rdfs:label ?v40_label .
  FILTER (lang(?v40_label) = 'ja')
  {
    SELECT ?v40 (COUNT(?gene) AS ?count)
    WHERE {
      {
        SELECT DISTINCT ?v40 ?gene (GROUP_CONCAT(DISTINCT ?tissue; separator=",") AS ?tissues)
        WHERE {
          ?gene refexo:affyProbeset/refexo:isPositivelySpecificTo ?v40 , ?tissue .
        }
      }
      FILTER (?tissues = str(?v40))
    }
  }
}
ORDER BY ?v40
